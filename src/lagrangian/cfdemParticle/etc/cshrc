#----------------------------------*-sh-*--------------------------------------
# CFDEMcoupling
# Christoph Goniva
# June 2012
#------------------------------------------------------------------------------
#
# Script
#     etc/cshrc
#
# Description
#     Startup file for cfdem exporting environment variables
#     Sourced  ~/.cshrc
#
#- adapt this and add to ./cshrc 
#- you can test the correctness using cfdemSystemTest.sh

##================================================#
##- source cfdem env vars
#setenv CFDEM_VERSION PUBLIC
#setenv CFDEM_PROJECT_DIR $HOME/CFDEM/CFDEMcoupling-$CFDEM_VERSION-$WM_PROJECT_VERSION
#setenv CFDEM_SRC_DIR $CFDEM_PROJECT_DIR/src/lagrangian/cfdemParticle
#setenv CFDEM_SOLVER_DIR $CFDEM_PROJECT_DIR/applications/solvers
#setenv CFDEM_DOC_DIR $CFDEM_PROJECT_DIR/doc
#setenv CFDEM_UT_DIR $CFDEM_PROJECT_DIR/applications/utilities
#setenv CFDEM_TUT_DIR $CFDEM_PROJECT_DIR/tutorials
#setenv CFDEM_PROJECT_USER_DIR $HOME/CFDEM/$LOGNAME-$CFDEM_VERSION-$WM_PROJECT_VERSION
#setenv CFDEM_bashrc $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/cshrc
#setenv CFDEM_LIGGGHTS_SRC_DIR $HOME/LIGGGHTS/LIGGGHTS-PUBLIC/src
#setenv CFDEM_LIGGGHTS_MAKEFILE_NAME fedora_fpic
#setenv CFDEM_LPP_DIR $HOME/LIGGGHTS/mylpp/src
#setenv CFDEM_PIZZA_DIR $HOME/LIGGGHTS/PIZZA/gran_pizza_17Aug10/src
#source $CFDEM_bashrc
#================================================#
#------------------------------------------------------------------------------

#------------------------------------------------------------------------------
#- export environment variables (adapt to your paths)
#------------------------------------------------------------------------------

#check if default lammps lib path should be used
if ( ! ($?CFDEM_LAMMPS_LIB_DIR) ) then
    setenv CFDEM_LAMMPS_LIB_DIR $CFDEM_LIGGGHTS_SRC_DIR"/../lib/"
else
    echo "using CFDEM_LAMMPS_LIB_DIR=$CFDEM_LAMMPS_LIB_DIR defined by user."
endif

#- LIGGGHTS lib name
setenv CFDEM_LIGGGHTS_LIB_NAME lmp_$CFDEM_LIGGGHTS_MAKEFILE_NAME

#- CFDEM lib name
setenv CFDEM_LIB_NAME lagrangianCFDEM-$CFDEM_VERSION-$WM_PROJECT_VERSION

#- CFDEM compressible lib name
setenv CFDEM_LIB_COMP_NAME lagrangianCFDEMcomp-$CFDEM_VERSION-$WM_PROJECT_VERSION

#check if additional libraries should be compiled together with solvers
if ( ! ($?CFDEM_ADD_LIBS_NAME) ) then
    setenv CFDEM_ADD_LIBS_NAME additionalLibs_$WM_PROJECT_VERSION
    echo "using CFDEM_ADD_LIBS_NAME=$CFDEM_ADD_LIBS_NAME ."
else
    echo "using CFDEM_ADD_LIBS_NAME=$CFDEM_ADD_LIBS_NAME defined by user."
endif
if ( ! ($?CFDEM_ADD_LIBS_DIR) ) then
    setenv CFDEM_ADD_LIBS_DIR $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc
else
    echo "using CFDEM_ADD_LIBS_DIR=$CFDEM_ADD_LIBS_DIR defined by user."
endif

# check addLibs path and file name
if ( ! -f $CFDEM_ADD_LIBS_DIR/$CFDEM_ADD_LIBS_NAME ) then
    echo "!!! ERROR !!!: CFDEM_ADD_LIBS_DIR/CFDEM_ADD_LIBS_NAME=$CFDEM_ADD_LIBS_DIR/$CFDEM_ADD_LIBS_NAME does not exist."
endif

#-----------------------------------------------------
# additional libraries

#- LMP Many2Many lib path and makefile
setenv CFDEM_Many2ManyLIB_PATH $CFDEM_SRC_DIR/lagrangian/cfdemParticle/subModels/dataExchangeModel/twoWayMany2Many/library
setenv CFDEM_Many2ManyLIB_MAKEFILENAME $CFDEM_LIGGGHTS_MAKEFILE_NAME

#- LMP M2M lib path
setenv CFDEM_M2MLIB_PATH $CFDEM_SRC_DIR/lagrangian/cfdemParticle/subModels/dataExchangeModel/twoWayM2M/library
setenv CFDEM_M2MLIB_MAKEFILENAME $CFDEM_LIGGGHTS_MAKEFILE_NAME

#- LMP POEMS lib path
setenv CFDEM_POEMSLIB_PATH $CFDEM_LIGGGHTS_SRC_DIR/../lib/poems
setenv CFDEM_POEMSLIB_MAKEFILENAME g++

#- path to test harness
setenv CFDEM_TEST_HARNESS_PATH $CFDEM_PROJECT_USER_DIR/log/logFilesCFDEM-$CFDEM_VERSION-$WM_PROJECT_VERSION

#- path to libraries
setenv CFDEM_LIB_DIR $FOAM_USER_LIBBIN

#- path to apps
setenv CFDEM_APP_DIR $FOAM_USER_APPBIN

#------------------------------------------------------------------------------
#- settings for lpp postproc tool
#------------------------------------------------------------------------------

#- nr of procs for lpp tool
setenv CFDEM_LPP_NPROCS 4

#- nr of procs for lpp tool
setenv CFDEM_LPP_CHUNKSIZE 1

#- shortcut to run lpp
alias lpp 'python -i $CFDEM_LPP_DIR/lpp.py --cpunum $CFDEM_LPP_NPROCS --chunksize $CFDEM_LPP_CHUNKSIZE \!:1'

#------------------------------------------------------------------------------
#- aliases for easy navigation (no changes necessary)
#------------------------------------------------------------------------------
#- shortcut to cfdem path
alias cfdem 'cd $CFDEM_PROJECT_DIR'

#- shortcut to src path
alias cfdemSrc 'cd $CFDEM_SRC_DIR'

#- shortcut to etc path
alias cfdemEtc 'cd $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc'

#- shortcut to tutorial path
alias cfdemTut 'cd $CFDEM_TUT_DIR'

#- shortcut to solver path
alias cfdemSol 'cd $CFDEM_SOLVER_DIR'

#- shortcut to utilities path
alias cfdemUt 'cd $CFDEM_UT_DIR'

#- shortcut to run path
alias cfdemRun 'cd $CFDEM_PROJECT_USER_DIR/run'

#- shortcut to user solver path
alias cfdemUsrSol 'cd $CFDEM_PROJECT_USER_DIR/applications/solvers'

#- shortcut to documentation path
alias cfdemDoc 'cd $CFDEM_DOC_DIR'

#- shortcut to open the doxygen with firefox
alias cfdemDox 'firefox $CFDEM_DOC_DIR/doxygen/html/index.html'

#- shortcut to LIGGGHTS path
alias cfdemLIG 'cd $CFDEM_LIGGGHTS_SRC_DIR'

#- shortcut to system test
alias cfdemSysTest 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/cfdemSystemTest.sh'

#- shortcut to pull LIGGGHTS
alias cfdemPullLIG 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/pullLIGGGHTS.sh'

#- shortcut to pull CFDEMcoupling
alias cfdemPullCFDEMcoupling 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/pullCFDEMcoupling.sh'

#- shortcut to clean CFDEM
alias cfdemCleanCFDEM 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/cleanCFDEMcoupling.sh'

#- shortcut to compile LIGGGHTS + sublibraries
alias cfdemCompLIG 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileLIGGGHTS.sh'

#- shortcut to compile LIGGGHTS' sublibraries
alias cfdemCompLIGlibs 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileLIGGGHTS_lib.sh'

#- shortcut to compile CFDEMcoupling +LIGGGHTS
alias cfdemCompCFDEMall 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileCFDEMcoupling_all.sh'

#- shortcut to compile CFDEMcoupling (src+solvers)
alias cfdemCompCFDEM 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileCFDEMcoupling.sh'

#- shortcut to compile CFDEMcoupling src
alias cfdemCompCFDEMsrc 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileCFDEMcoupling_src.sh'

#- shortcut to compile CFDEMcoupling solvers
alias cfdemCompCFDEMsol 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileCFDEMcoupling_sol.sh'

#- shortcut to compile CFDEMcoupling utilities
alias cfdemCompCFDEMuti 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/compileCFDEMcoupling_uti.sh'

#- shortcut to test basic tutorials
alias cfdemTestTUT 'bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/testTutorials.sh'

#- shortcut to change dictionaries according to OF version
#cfdemChangeTutOFversion() { bash $CFDEM_SRC_DIR/lagrangian/cfdemParticle/etc/OFVersionChange/shellScripts/cfdemChangeTutOFversion_all.sh $1; }
#export -f cfdemChangeTutOFversion

#- shortcut to visualize the clock model data
alias vizClock 'python $CFDEM_UT_DIR/vizClock/matPlot.py'

#- shortcut to run liggghts in serial
alias cfdemLiggghts '$CFDEM_LIGGGHTS_SRC_DIR/lmp_$CFDEM_LIGGGHTS_MAKEFILE_NAME'

#- shortcut to run liggghts in parallel (no fcts in csh...)
alias cfdemLiggghtsPar 'echo "mpirun -np xx -machinefile mynodes $CFDEM_LIGGGHTS_SRC_DIR/lmp_$CFDEM_LIGGGHTS_MAKEFILE_NAME < in.liggghts_init"'

#- shortcut to run liggghts in parallel
# unfortunately no functions available in csh

#- shortcut to run lpp
alias lpp 'python -i $CFDEM_LPP_DIR/lpp.py \!:1'

# check if the directory exists
if ( -d "$CFDEM_PROJECT_USER_DIR" ) then
    :
else
    if ( -d "$CFDEM_PROJECT_DIR" ) then
        echo "make new dirs $CFDEM_PROJECT_USER_DIR ? (y/n)"
	set YN=$<
        if ( $YN == "y" ) then
            mkdir -p $CFDEM_PROJECT_USER_DIR
            cd $CFDEM_PROJECT_USER_DIR
            mkdir run
            mkdir -p log/logFilesCFDEM-$CFDEM_VERSION-$WM_PROJECT_VERSION
            mkdir -p applications/solvers
        else
            echo "aborted by user."
            exit
        endif
    else
        echo "error in CFDEMcoupling's cshrc."
        exit
    endif
endif

# detect OF version
if ( $WM_PROJECT_VERSION =~ "4.*" ) then
    setenv CFDEM_WM_PROJECT_VERSION 40
    echo "************************************"
    echo "WARNING from your CFDEM code!"
    echo "WARNING: Coupling with your OpenFOAM(R) version is currently under development!"
    echo "In doubt switch to another version."
    echo "************************************"
    sleep 1.5
else if ( $WM_PROJECT_VERSION =~ "3.0.*" ) then
    setenv CFDEM_WM_PROJECT_VERSION 30
else if ( $WM_PROJECT_VERSION =~ "2.4.*" ) then
    setenv CFDEM_WM_PROJECT_VERSION 24
else if ( $WM_PROJECT_VERSION == "3.2" ) then
    if ( $WM_FORK == "extend" ) then
        setenv CFDEM_WM_PROJECT_VERSION 132
    endif
else
    clear
    echo "************************************"
    echo "ERROR from your CFDEM code!"
    echo "ERROR: your openFOAM(R)s version is not supported!"
    echo "Please use a version that is supported, i.e., see the bashrc file in the source directory!"
    echo "************************************"
    sleep 1.5
endif
